Strict

Private
Import vortex

Public
Class Grid Final
	Const NumRows:Int = 3
	Const NumColumns:Int = 3
	
	Function Create:Grid(rows:Int[][])
		If CheckRows(rows)
			Return New Grid(rows)
		Else
			Return Null	
		End
	End
	
	Method MoveTile:Bool(column:Int, row:Int)
		Return False
	End
	
	Method Model:Model(column:Int, row:Int)
		Return mRows[row][column]
	End
Private
	Field mRows:Model[][]
	
	Function CheckRows:Bool(rows:Int[][])
		If rows.Length = NumRows
			For Local row:Int[] = Eachin rows
				If Not CheckRow(row) Then Return False
			Next
			Return True
		Else
			Return False
		End
	End
	
	Function CheckRow:Bool(row:Int[])
		If row.Length = NumColumns
			Return True
		Else
			Return False
		End
	End
	
	Method New(rows:Int[][])
		mRows = New Model[0][]
		For Local i:Int = 0 Until rows.Length
			mRows = Append(mRows, ParseRow(rows[i], i))
		Next
	End
	
	Function Append:Model[][](arr:Model[][], row:Model[])
		arr = arr.Resize(arr.Length + 1)
		arr[arr.Length - 1] = row.Resize(row.Length)
		Return arr
	End
	
	Function ParseRow:Model[](row:Int[], rowIndex:Int)
		Local modelRow:Model[] = New Model[row.Length]
		For Local i:Int = 0 Until row.Length
			modelRow[i] = LoadModel(row[i], rowIndex, i)
		Next
		Return modelRow
	End
	
	Function LoadModel:Model(id:Int, rowIndex:Int, colIndex:Int)
		If id <> 0
			Local model:Model = New Model(LoadMesh(id))
			model.Position(colIndex, 0, NumRows - rowIndex - 1)
			model.Pickable(True)
			model.Material.Color = Color.White
			Return model
		Else
			Return Null
		End
	End
	
	Function LoadMesh:Mesh(id:Int)
		Local filename:String = "roadTile_" + id + ".msh.dat"
		Local wasLoaded:Bool = Mesh.IsLoaded(filename)
		Local mesh:Mesh = Mesh.Load(filename)
		If Not wasLoaded
			Local max:Float = Max(Max(mesh.Width, mesh.Height), mesh.Depth)
			Local offsetX:Float = (mesh.BoxMaxX - mesh.BoxMinX) / 2
			Local offsetY:Float = (mesh.BoxMaxY - mesh.BoxMinY) / 2
			Local offsetZ:Float = (mesh.BoxMaxZ - mesh.BoxMinZ) / 2
			mesh.Translate(offsetX, offsetY, offsetZ)
			mesh.Scale(1 / max, 1 / max, 1 / max)
		End
		Return mesh
	End
	
	Method New()
	End
End
