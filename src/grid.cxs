Strict


Private
Import brl.filepath
Import tile
Import vortex

Public
Class Grid Final	
	Function Create:Grid(rows:Int[][])
		InitQuad()
		If CheckRows(rows)
			Return New Grid(rows)
		Else
			Return Null	
		End
	End
	
	Method Discard:Void()
		For Local row:Model[] = Eachin mRows
			For Local model:Model = Eachin row
				If model <> Null Then model.Discard()
			Next
		Next
	End
	
	Method MoveTile:Bool(tile:Tile)
		Local adjacent:Tile = EmptyAdjacentTile(tile)
		If adjacent <> Null
			Local model:Model = Self.ModelAt(tile)
			mRows[adjacent.Row][adjacent.Column] = model
			mRows[tile.Row][tile.Column] = Null
			model.Position(adjacent.X, 0, adjacent.Z)
			Return True
		Else
			Return False
		End
	End
	
	Method ModelAt:Model(tile:Tile)
		If tile.IsInner()
			Return mRows[tile.Row][tile.Column]
		Else
			Return Null
		End
	End
	
	Method TileExit:Int(tile:Tile, lastExit:Int)
		Local exits:Int[] = AllExitsForTile(tile)
		Local excludeExit:Int = AdjacentExit(lastExit)
		If excludeExit <> -1
			If exits.Length >= 2 And exits[1] = excludeExit Then exits = exits[0 .. 1]
			If exits.Length >= 1 And exits[0] = excludeExit Then exits = exits[1 .. exits.Length]
		End
		If exits.Length > 0
			Local nextTile:Tile = NextTile(tile, exits[0])
			If TileHasExit(nextTile, AdjacentExit(exits[0]))
				Return exits[0]
			Else
				Return -1
			End
		Else
			Return -1
		End
	End
	
	Method NextTile:Tile(fromTile:Tile, selectedExit:Int)
		Local nextTile:Tile = Null
		Select selectedExit
		Case Left
			Return New Tile(fromTile.Column - 1, fromTile.Row)
		Case Right
			Return New Tile(fromTile.Column + 1, fromTile.Row)
		Case Top
			Return New Tile(fromTile.Column, fromTile.Row - 1)
		Case Bottom
			Return New Tile(fromTile.Column, fromTile.Row + 1)
		Default
			Return Null
		End
	End
Private
	Const Left:Int = 2
	Const Right:Int = 0
	Const Top:Int = 1
	Const Bottom:Int = 3
	
	Global mQuad:Mesh
	Field mRows:Model[][]
	
	Function InitQuad:Void()
		If mQuad = Null
			mQuad = Mesh.CreateQuad()
			mQuad.Rotate(90, 0, 0)
			mQuad.Rebuild()
		End
	End
	
	Function CheckRows:Bool(rows:Int[][])
		If rows.Length = Tile.NumRows
			For Local row:Int[] = Eachin rows
				If Not CheckRow(row) Then Return False
			Next
			Return True
		Else
			Return False
		End
	End
	
	Function CheckRow:Bool(row:Int[])
		If row.Length = Tile.NumColumns
			Return True
		Else
			Return False
		End
	End
	
	Method New(rows:Int[][])
		mRows = New Model[0][]
		For Local i:Int = 0 Until rows.Length
			mRows = Append(mRows, ParseRow(rows[i], i))
		Next
	End
	
	Function Append:Model[][](arr:Model[][], row:Model[])
		arr = arr.Resize(arr.Length + 1)
		arr[arr.Length - 1] = row.Resize(row.Length)
		Return arr
	End
	
	Function ParseRow:Model[](row:Int[], rowIndex:Int)
		Local modelRow:Model[] = New Model[row.Length]
		For Local i:Int = 0 Until row.Length
			modelRow[i] = LoadModel(row[i], rowIndex, i)
		Next
		Return modelRow
	End
	
	Function LoadModel:Model(id:Int, rowIndex:Int, colIndex:Int)
		If id <> 0
			Local tile:Tile = New Tile(colIndex, rowIndex)
			Local model:Model = New Model(mQuad)
			model.Material(0).ColorTexture = Texture.Load(LPad(id, "0", 2) + ".png", Texture.FilterNone)
			model.Position(tile.X, 0, tile.Z)
			model.Pickable(True)
			Return model
		Else
			Return Null
		End
	End
	
	Function LPad:String(str:String, char:String, len:Int)
		char = char[0..1]
		Local amount:Int = Max(0, len - str.Length)
		Local lpad:String = ""
		For Local i:Int = 0 Until amount
			lpad += char
		Next
		Return lpad + str
	End
	
	Method EmptyAdjacentTile:Tile(tile:Tile)
		Local left:Tile = New Tile(tile.Column - 1, tile.Row)
		Local right:Tile = New Tile(tile.Column + 1, tile.Row)
		Local up:Tile = New Tile(tile.Column, tile.Row - 1)
		Local down:Tile = New Tile(tile.Column, tile.Row + 1)
		If IsEmptyTile(left)
			Return left
		Elseif IsEmptyTile(right)
			Return right
		Elseif IsEmptyTile(up)
			Return up
		Elseif IsEmptyTile(down)
			Return down
		Else
			Return Null
		End
	End
	
	Method IsEmptyTile:Bool(tile:Tile)
		If tile.Row >= 0 And tile.Row < Tile.NumRows And tile.Column >= 0 And tile.Column < Tile.NumColumns And Self.ModelAt(tile) = Null
			Return True
		Else
			Return False
		End
	End
	
	Method TileHasExit:Bool(tile:Tile, exit_:Int)
		Local exits:Int[] = AllExitsForTile(tile)
		For Local ex:Int = Eachin exits
			If ex = exit_ Then Return True
		Next
		Return False
	End
	
	Method AllExitsForTile:Int[](tile:Tile)
		Local model:Model = ModelAt(tile)
		Local exits:Int[] = []
		If model <> Null
			Local id:Int = Int(StripExt(model.Material(0).ColorTexture.Filename))
			Local exitsMap:IntMap<Int[]> = ExitsMap()
			If exitsMap.Contains(id) Then exits = exitsMap.Get(id)
		End
		If exits.Length = 0 And Not tile.IsInner()
			If tile.Column < 0
				exits = [Right]
			Elseif tile.Column >= Tile.NumColumns
				exits = [Left]
			Elseif tile.Row < 0
				exits = [Bottom]
			Else
				exits = [Top]
			End
		End
		Return exits
	End
	
	Function ExitsMap:IntMap<Int[]>()
		Local exitsMap:IntMap<Int[]> = New IntMap<Int[]>()
		exitsMap.Add(1, [Top, Bottom])
		exitsMap.Add(2, [Left, Right])
		exitsMap.Add(3, [Right, Bottom])
		exitsMap.Add(4, [Left, Bottom])
		exitsMap.Add(5, [Left, Top])
		exitsMap.Add(6, [Right, Top])
		Return exitsMap
	End
	
	Function AdjacentExit:Int(selectedExit:Int)
		Select selectedExit
		Case Left
			Return Right
		Case Right
			Return Left
		Case Top
			Return Bottom
		Case Bottom
			Return Top
		Default
			Return -1
		End
	End
	
	Method New()
	End
End
