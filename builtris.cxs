Strict

#GLFW_WINDOW_TITLE="Builtris"
#GLFW_WINDOW_WIDTH=512
#GLFW_WINDOW_HEIGHT=512
#GLFW_WINDOW_RESIZABLE=True
#GLFW_WINDOW_SAMPLES=0

Import mojo.app
Import mojo.input
Import vortex
Import src.animator
Import src.car
Import src.flagpole
Import src.grid
Import src.positionanimator
Import src.tile

Const SCREEN_SIZE:Int = 64

Class BuiltrisApp Extends App Implements AnimatorDelegate
	Method OnCreate:Int()
		SetUpdateRate(20)
		SetSwapInterval(True)
		Seed = Millisecs()
		If Not World.Init() Then EndApp()
		World.SunRotation(45, -135)
		
		mFont = Font.Load("8-bit-hud_8.fnt.dat")
		
		mCam = New Camera()
		mCam.FovY = 35
		mCam.Viewport(0, 0, SCREEN_SIZE, SCREEN_SIZE)
		mCam.Framebuffer = New Framebuffer(SCREEN_SIZE, SCREEN_SIZE, True)
		mCam.Position(Tile.NumColumns / 2, 0, Tile.NumRows / 2)
		mCam.Rotate(45, -45, 0)
		mCam.Move(0, 0, -7)
		mCam.Position(mCam.WorldX, mCam.WorldY - 0.5, mCam.WorldZ)
		
		mGrid = Grid.Create([
			[3, 2, 4],
			[1, 0, 1],
			[6, 2, 5]])
		
		
		mCarTile = New Tile(1, 3)
		mCar = New Car(mCarTile)
		
		mFlagTile = New Tile(1, -1)
		mFlag = New FlagPole(mFlagTile)
		
		mButton = Texture.Load("run.png")
		
		mTilePath = Null
		
		Return False
	End
	
	Method OnUpdate:Int()
		'End with escape key
		#If TARGET<>"html5"
		If KeyHit(KEY_ESCAPE) Then EndApp()
		#End
		
		UpdatePick()
		If UpdateButton()
			SetPathToFlag()
			CreateCarAnimatorToNextNode()
		End
		Animator.UpdateAll()
		World.Update()
		
		Return False
	End
	
	Method OnRender:Int()
		'Draw world to framebuffer
		mScreenRect = ScreenRect.Calculate()
		World.Render()
		
		'Draw UI to framebuffer
		mCam.Framebuffer.Use()
		Graphics.Setup2D(0, 0, SCREEN_SIZE, SCREEN_SIZE)
		If mTilePath = Null
			If mButtonHovered Then Graphics.Color(Color.Green) Else Graphics.Color(Color.Multiply(Color.Green, 0.5))
			mButton.Draw(SCREEN_SIZE - 8, SCREEN_SIZE - 12)
		End
		Framebuffer.UseScreen()
		
		'Draw framebuffer to screen
		Graphics.Setup2D(0, 0, DeviceWidth(), DeviceHeight())
		Graphics.Clear()
		mCam.Framebuffer.ColorTexture.Draw(mScreenRect.X, mScreenRect.Y, mScreenRect.Width, -mScreenRect.Height)
		Return False
	End
	
	Method OnAnimatorEnd:Void(animator:Animator)
		If Not CreateCarAnimatorToNextNode()
			Print("Done")
		End
	End
Private
	Field mFont:Font
	Field mCam:Camera
	Field mGrid:Grid
	Field mCarTile:Tile
	Field mCar:Car
	Field mFlagTile:Tile
	Field mFlag:FlagPole
	Field mButton:Texture
	Field mButtonHovered:Bool
	Field mTilePath:List<Tile>
	Field mScreenRect:ScreenRect
	
	Method UpdatePick:Void()
		Local ratio:Float = SCREEN_SIZE * 1.0 / mScreenRect.Width
		If MouseHit(MOUSE_LEFT) And World.CameraPick(mCam, (MouseX() - mScreenRect.X) * ratio, (MouseY() - mScreenRect.Y) * ratio)
			mGrid.MoveTile(Tile.FromEntity(World.PickedEntity()))
		End
	End
	
	Method UpdateButton:Bool()
		mButtonHovered = MouseX() >= mScreenRect.X + mScreenRect.Width - (mScreenRect.Width / 4) And MouseY() >= mScreenRect.Y + mScreenRect.Height - (mScreenRect.Height / 4)
		If MouseHit(MOUSE_LEFT) And mButtonHovered And mTilePath = Null
			Return True
		Else
			Return False
		End
	End
	
	Method SetPathToFlag:Void()
		If mTilePath = Null
			Local currentTile:Tile = mCarTile
			Local currentExit:Int = mGrid.TileExit(currentTile, -1)
			mTilePath = New List<Tile>()
			While Not currentTile.Equals(mFlagTile)
				If Not currentTile.Equals(mCarTile) Then mTilePath.AddLast(currentTile)
				currentTile = mGrid.NextTile(currentTile, currentExit)
				If currentTile = Null Then Exit
				currentExit = mGrid.TileExit(currentTile, currentExit)
			Wend
			If currentTile <> Null And currentTile.Equals(mFlagTile) Then mTilePath.AddLast(currentTile)
		End
	End
	
	Method CreateCarAnimatorToNextNode:Bool()
		If Not mTilePath.IsEmpty()
			Local nextTile:Tile = mTilePath.RemoveFirst()
			mCar.RotateToTile(nextTile)
			New PositionAnimator(mCar.Entity, nextTile.X, 0, nextTile.Z, 1, Self)
			Return True
		Else
			Return False
		End
	End
End

Function Main:Int()
	New BuiltrisApp
	Return False
End

Class ScreenRect
	Field X:Int
	Field Y:Int
	Field Width:Int
	Field Height:Int
	
	Function Calculate:ScreenRect()
		Local rect:ScreenRect = New ScreenRect
		If DeviceWidth() > DeviceHeight()
			Local ratio:Float = Float(DeviceHeight()) / DeviceWidth()
			rect.Width = DeviceWidth() * ratio
			rect.Height = DeviceHeight()
			rect.X = (DeviceWidth() - rect.Width) / 2
			rect.Y = 0
		Else
			Local ratio:Float = Float(DeviceWidth()) / DeviceHeight()
			rect.Width = DeviceWidth()
			rect.Height = DeviceHeight() * ratio
			rect.X = 0
			rect.Y = (DeviceHeight() - rect.Height) / 2
		End
		Return rect
	End
End